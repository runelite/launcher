import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'java'
    id "com.github.ben-manes.versions" version "0.22.0"
    id 'se.patrikerdes.use-latest-versions' version '0.2.8'
}

apply plugin: 'java-library'
apply plugin: 'checkstyle'

group = 'net.runelite'
version = '2.0.5'
description = 'OpenOSRS Launcher'

repositories {
    mavenLocal()
    maven {
        url = 'https://repo1.maven.org/maven2'
    }

    maven {
        url = 'https://raw.githubusercontent.com/open-osrs/maven-repo/master'
    }
}

dependencies {
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'


    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.10'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '5.0.4'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation group: 'com.google.guava', name: 'guava', version: '28.2-jre'
    implementation group: 'com.vdurmont', name: 'semver4j', version: '3.1.0'

    testImplementation group: 'junit', name: 'junit', version: '4.13'
}

processResources {
    copy {
        from file("src/main/resources/launcher.properties"), {
            filter(ReplaceTokens, tokens: [
                    "project.version": project.version
            ])
        }
        into("${rootDir}/build/resources/main/net/runelite/launcher/")
    }

    copy {
        from file("${rootDir}/packr/Info.plist"), {
            filter(ReplaceTokens, tokens: [
                    "finalName"       : "OpenOSRS",
                    "artifact"        : "launcher",
                    "project.version" : project.version,
                    "project.group"   : project.group
            ])
        }

        from file("${rootDir}/innosetup/openosrs.iss"), {
            filter(ReplaceTokens, tokens: [
                    "project.version" : project.version,
                    "basedir"         : project.projectDir.path
            ])
        }

        from file("${rootDir}/innosetup/openosrs32.iss"), {
            filter(ReplaceTokens, tokens: [
                    "project.version" : project.version,
                    "basedir"         : project.projectDir.path
            ])
        }

        from file("${rootDir}/appimage/openosrs.desktop"), {
            filter(ReplaceTokens, tokens: [
                    "finalName"       : "OpenOSRS",
                    "description"     : "OpenOSRS launcher"
            ])
        }
        into("${rootDir}/build/filtered-resources/")
    }
}

checkstyle {
    toolVersion = '6.4.1'
    sourceSets = [sourceSets.main]
    configFile = file("${rootDir}/checkstyle/checkstyle.xml")
    configProperties = [ "suppressionFile" : file("${rootDir}/checkstyle/suppressions.xml")]
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
}

def isNonStable = { String version ->
    def unstableKeyword = ['ALPHA', 'BETA', 'RC'].any { it -> version.toUpperCase().contains(it) }

    return unstableKeyword
}

dependencyUpdates {
    checkForGradleUpdate = false

    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(candidate.version)) {
                    reject()
                }
            }
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'net.runelite.launcher.Launcher'
    }
}

shadowJar {
    archiveName 'OpenOSRS-shaded.jar'

    exclude("net/runelite/injector/**")
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task copyToLib(type: Copy) {
    into "$buildDir/lib"
    from configurations.runtime
}

tasks.build.dependsOn tasks.shadowJar
tasks.shadowJar.dependsOn tasks.copyToLib
